plugins {
    id('java')
    id('application')
    id('org.beryx.jlink') version "$beryxPluginVersion"
    id('com.gradleup.shadow') version "$shadowPluginVersion"
    id('org.gradlex.extra-java-module-info') version "$extraModuleInfoPluginVersion"
}

version = rootProject.version
group = 'net.brlns'

def author = 'Gabriel D @hstr0100'
def lowercaseName = 'gdownloader'

def os = org.gradle.internal.os.OperatingSystem.current()

def getArchitecture(def os) {
    def arch = System.getProperty("os.arch").toLowerCase()
    if (arch.contains("aarch64") || arch.contains("arm64")) {
        return "arm64"
    } else if (arch.contains("arm")) {
        return os.isLinux() ? "armhf" : "arm"
    } else if (arch.contains("64")) {
        return os.isLinux() ? "amd64" : "x64"
    } else {
        return os.isLinux() ? "i386" : "x86"
    }
}

def architecture = getArchitecture(os)

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }

    // There seems to be no consensus whatsoever on whether to use .set() or =.
    // Personally, I prefer = for consistency, and .set() for a cleaner-looking syntax.
    // Perhaps we can settle on a mix of both approaches.

    modularity.inferModulePath.set(true)
}

application {
    mainClass.set('net.brlns.gdownloader.GDownloader')
    mainModule.set('net.brlns.gdownloader')
    applicationDefaultJvmArgs = ['--add-exports=java.management/sun.management=ALL-UNNAMED']
}

dependencies {
    implementation(project(path: ':twelvemonkeys', configuration: 'moduleJar'))
    implementation("ch.qos.logback:logback-classic:$logbackVersion")
    implementation("com.github.kwhat:jnativehook:$jnativehookVersion")
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    implementation("org.jsoup:jsoup:$jsoupVersion")
    implementation("org.slf4j:slf4j-api:$slf4jVersion")
    implementation("net.java.dev.jna:jna:$jnaVersion")
    implementation("net.java.dev.jna:jna-platform:$jnaVersion")
    implementation("jakarta.annotation:jakarta.annotation-api:$jakartaAnnotationVersion")
    implementation("jakarta.persistence:jakarta.persistence-api:$jakartaPersistenceVersion")
    implementation("org.eclipse.persistence:eclipselink:$eclipselinkVersion")
    implementation("org.hsqldb:hsqldb:$hsqldbVersion")
    implementation("com.adonax:audiocue:$adonaxVersion")
    implementation("com.github.hypfvieh:dbus-java-core:$dbusVersion")
    implementation("com.github.hypfvieh:dbus-java-transport-native-unixsocket:$dbusVersion")

    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
    testImplementation("org.junit.jupiter:junit-jupiter:$jupiterVersion")
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

test {
    useJUnitPlatform()
}

run {
    enableAssertions = true
}

tasks.named('run') {
    jvmArgs = ['-Xss5M']
}

task generateVersionFile {
    group = 'build'

    def versionFile = file("src/main/resources/version.properties")

    inputs.property("version", project.version)
    outputs.file(versionFile)

    doLast {
        versionFile.parentFile.mkdirs()
        versionFile.text = "version=${project.version}"
    }
}

compileJava.dependsOn generateVersionFile
processResources.dependsOn generateVersionFile

extraJavaModuleInfo {
    failOnMissingModuleInfo.set(true)
    deriveAutomaticModuleNamesFromFileNames.set(true)

    module("twelvemonkeys-$twelvemonkeysVersion-all.jar", 'com.twelvemonkeys.imageio') {
        requiresStatic('java.desktop')

        exports('com.twelvemonkeys.imageio.stream')
        exports('com.twelvemonkeys.imageio.color')
        exports('com.twelvemonkeys.imageio.plugins.jpeg')
        exports('com.twelvemonkeys.imageio.plugins.webp')
        exports('com.twelvemonkeys.imageio.plugins.tiff')
    }

    automaticModule('net.java.dev.jna:jna', 'com.sun.jna') {
        overrideModuleName()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(['-Xlint:all,-serial,-processing,-requires-automatic,-missing-explicit-ctor'])
}

jar {
    archiveBaseName.set("${lowercaseName}-java")

    manifest {
        attributes(
            'Implementation-Title': rootProject.name,
            'Implementation-Version': version,
            'Implementation-Vendor': author,
            'Main-Class': application.mainClass.get()
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    archiveBaseName.set("${lowercaseName}-java")
    mergeServiceFiles()
}

// Workaround for https://github.com/gradle/gradle/issues/26155 and https://github.com/johnrengelman/shadow/issues/882
tasks.named('shadowJar') { shadowJar ->
    inputs.files(project.configurations.runtimeClasspath)
    configurations = []

    doFirst {
        configurations = [project.configurations.runtimeClasspath]
    }
}

task zipJpackageImage(type: Zip, dependsOn: jpackage) {
    def fileName = os.isWindows() ? "${lowercaseName}-${version}-windows_portable_${architecture}.zip" :
                   os.isLinux() ? "${lowercaseName}-${version}-linux_portable_${architecture}.zip" :
                   "${lowercaseName}-${version}-mac_portable_${architecture}.zip"

    archiveFileName.set(fileName)
    destinationDirectory.set(file("$buildDir/jpackage"))

    // A less-than-stellar but functional way to detect that the program is running in portable mode.
    doFirst {
        def basePath = os.isWindows() ? "$buildDir/jpackage/${rootProject.name}" :
                       os.isLinux() ? "$buildDir/jpackage/${rootProject.name}/lib" :
                       "$buildDir/jpackage/${rootProject.name}.app/Contents"

        def portableLockFile = file("$basePath/runtime/portable.lock")
        portableLockFile.text = [
            "This file ensures that the program will run in portable mode.",
            "Deleting it when the program is not installed is not recommended."
        ].join("\n")
    }

    from("$buildDir/jpackage/${rootProject.name}")
}

tasks.jpackage.finalizedBy zipJpackageImage

jlink {
    options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'GDownloader'
        jvmArgs = ['-Xmx2G', '-Xss5M']
        noConsole = true
    }

    jpackage {
        jvmArgs = ['-Djnativehook.lib.locator=net.brlns.gdownloader.util.CustomNativeLocator']

        def imgType = os.isWindows() ? 'ico' : os.isMacOsX() ? 'icns' : 'png'
        icon = "src/main/resources/assets/launcher/GDownloader.$imgType"

        installerOptions += [
            '--about-url', 'https://github.com/hstr0100/GDownloader',
            '--description', 'Graphical interface for yt-dlp, gallery-dl, and spotDL',
            '--vendor', author,
            '--copyright', "2025 ${author}",
            '--license-file', "../LICENSE",
            '--app-version', version
        ]

        if (os.isWindows()) {
            installerName = "${lowercaseName}-windows-${architecture}"
            installerType = 'msi'
            //imageOptions += ['--win-console']
            installerOptions += [
                '--win-per-user-install',
                '--win-dir-chooser',
                '--win-menu',
                '--win-menu-group', "${rootProject.name}",
                '--win-shortcut'
            ]
        } else if (os.isLinux()) {
            // AppImage not yet supported by jpackage
            installerType = 'deb'
            installerOptions += [
                '--resource-dir', "build/jpackage/${rootProject.name}/lib/app",
                '--linux-package-name', "${lowercaseName}-linux",
                '--linux-app-category', 'web',
                '--linux-menu-group', 'Network;',
                '--linux-deb-maintainer', 'brokenlensllc@gmail.com',
                '--linux-rpm-license-type', 'GPLv3',
                '--linux-shortcut'
            ]
        } else if (os.isMacOsX()) {
            installerName = "${lowercaseName}-mac-${architecture}"
            installerType = 'dmg'
            installerOptions += [
                '--mac-package-name', "${rootProject.name}"
            ]
        }
    }
}

tasks.jpackage.doFirst {
    def basePath = os.isWindows() ? rootProject.name :
                   os.isLinux() ? "${rootProject.name}/lib" :
                   "${rootProject.name}.app/Contents"

    copy {
        from('src/main/resources/assets/launcher')
        include('GDownloader.png')
        include('GDownloader.icns')
        include('GDownloader.ico')
        include('GDownloader.desktop')
        into("$buildDir/jpackage/$basePath/app")
    }
}

configurations {
    resolvableImplementation { extendsFrom implementation }
}

tasks.jlink.doLast {
    configurations.resolvableImplementation.files.findAll { f ->
        f.name.contains('jnativehook')
    }.each { nativeJar ->
        def destDir = file("$imageDir/bin/native-libs")
        delete(destDir)
        mkdir(destDir)

        def baseLibPath = 'com/github/kwhat/jnativehook/lib'
        def zt = zipTree(nativeJar).matching {
            include("$baseLibPath/**")
        }

        def expandedPath = zt.asFileTree.tree.mirror.dir.path
        zt.files.each { f ->
            def pathInLib = (f.path - expandedPath).substring(1)
            pathInLib = pathInLib.replace('\\', '/')
            pathInLib -= "$baseLibPath/"
            def destFilePath = destDir.toPath().resolve(pathInLib)
            mkdir(destFilePath.parent)
            java.nio.file.Files.copy(f.toPath(), destFilePath)
        }
    }
}

task createAppImage(type: Exec, dependsOn: zipJpackageImage) {
    group = 'distribution'
    onlyIf { os.isLinux() }

    def appImageArch
    def nativeLibsArch
    if (architecture == "arm64" || architecture == "aarch64") {
        appImageArch = "aarch64"
        nativeLibsArch = "arm64"
    } else if (architecture == "arm" || architecture == "armhf") {
        appImageArch = "armhf"
        nativeLibsArch = "arm"
    } else if (architecture == "x64" || architecture == "amd64") {
        appImageArch = "x86_64"
        nativeLibsArch = "x86_64"
    } else {
        appImageArch = "i686"
        nativeLibsArch = "x86"
    }

    def appGroup = 'net.brlns'
    def appDirBase = file("$buildDir/appimage")

    doFirst {
        def appDir = file("$appDirBase/AppDir")
        def appUsrPath = "$appDir/usr"
        def appLibPath = "$appUsrPath/lib"

        delete(appDirBase)
        mkdir(appDir)

        copy {
            from file("$buildDir/jpackage/${rootProject.name}")
            into appUsrPath
        }

        // TODO: different update endpoint for AppImage
        file("$appLibPath/runtime/portable.lock")
            .renameTo(file("$appLibPath/runtime/appimage.lock"))

        file("$appUsrPath/share/metainfo").mkdirs()
        file("$appUsrPath/share/applications").mkdirs()
        file("$appUsrPath/share/icons/hicolor/512x512/apps").mkdirs()

        def today = new java.text.SimpleDateFormat("yyyy-MM-dd").format(new Date())

        [
            "${appGroup}.${lowercaseName}.metainfo.xml",
            "${appGroup}.${lowercaseName}.appdata.xml",
        ].each { destFileName ->
            copy{
                from file("src/main/resources/${lowercaseName}.appdata.xml")
                into "$appUsrPath/share/metainfo"
                rename { destFileName }

                filter { line ->
                    line.replace("{version}", rootProject.version)
                        .replace("{timestamp}", today)
                }
            }
        }

        // Include libxkbcommon-x11 in AppImage if available
        def libDirs = [
            "/usr/lib/x86_64-linux-gnu",
            "/usr/lib/i386-linux-gnu",
            "/usr/lib/aarch64-linux-gnu",
            "/usr/lib/arm-linux-gnueabihf",
            "/usr/lib"
        ]

        def foundXkbCommon = false

        libDirs.each { libDirPath ->
            if (!foundXkbCommon) {
                def libDir = file(libDirPath)
                def libFile = file("${libDir}/libxkbcommon-x11.so.0")

                if (libFile.exists()) {
                    copy {
                        from libFile
                        into "$appLibPath/runtime/bin/native-libs/linux/${nativeLibsArch}"
                    }

                    foundXkbCommon = true
                    logger.lifecycle("Found libxkbcommon-x11.so.0 in ${libDir}")
                }
            }
        }

        if (!foundXkbCommon) {
            logger.warn("Could not find libxkbcommon-x11.so.0 in any of: ${libDirs}")
        }

        def launcherScript = file("$appLibPath/${rootProject.name}")
        launcherScript.setExecutable(true, false)

        def iconPath = "$appLibPath/app/GDownloader.png"

        [
            "$appDir/${appGroup}.${lowercaseName}.png",
            "$appUsrPath/share/icons/hicolor/512x512/apps/${appGroup}.${lowercaseName}.png",
            "$appDir/.DirIcon"
        ].each { destPath ->
            copy {
                from iconPath
                into file(destPath).parent
                rename { file(destPath).name }
            }
        }

        [
            "$appUsrPath/share/applications/${appGroup}.${lowercaseName}.desktop",
            "$appDir/${appGroup}.${lowercaseName}.desktop",
        ].each { destPathString ->
            def destFile = file(destPathString)
            def parentDir = destFile.parentFile

            parentDir.mkdirs()

            copy {
                from file("src/main/resources/assets/launcher/${rootProject.name}.desktop")
                into parentDir

                rename { destFile.name }

                filter { line ->
                    line.replace("APPLICATION_LAUNCHER", rootProject.name)
                        .replace("APPLICATION_ICON", "${appGroup}.${lowercaseName}")
                }
            }

            if (destFile.exists()) {
                 destFile.setExecutable(true, false)
            }
        }

        def appRun = file("$appDir/AppRun")
        appRun.text = [
            "#!/bin/bash",
            "HERE=\"\$(dirname \"\$(readlink -f \"\${0}\")\")\"",
            "NATIVES=\"\${HERE}/usr/lib/runtime/bin/native-libs/linux/${nativeLibsArch}\"",
            "export PATH=\"\${HERE}/usr/bin:\${PATH}\"",
            "export LD_LIBRARY_PATH=\"\${HERE}/usr/lib:\${NATIVES}" + '${LD_LIBRARY_PATH:+:\${LD_LIBRARY_PATH}}\"',
            "exec \"\${HERE}/usr/bin/${rootProject.name}\" \"\$@\""
        ].join("\n")
        appRun.setExecutable(true, false)
    }

    // Build image
    // We were using a lowercase name due to convention; .deb files require lowercase names, and it would look out of place and trigger OCD.
    def appImageName = "${rootProject.name}-${version}-${appImageArch}.AppImage"
    def updateInfo = "gh-releases-zsync|hstr0100|${rootProject.name}|latest|${rootProject.name}-*${appImageArch}.AppImage.zsync"

    workingDir "$buildDir/appimage"
    environment "ARCH", appImageArch

    def gpgKeyId = System.getenv('GPG_KEY_ID')
    // this requires appimagetool (without extension and version number) to be in your PATH
    // https://github.com/AppImage/appimagetool/releases
    def commandLineArgs = ["appimagetool", "--verbose", "-n", "AppDir", appImageName,
        "--updateinformation", updateInfo]

    if (gpgKeyId) {
        commandLineArgs += ["--sign", gpgKeyId]
    }

    commandLine commandLineArgs

    doLast {
        [
            file("$buildDir/appimage/${appImageName}"),
            file("$buildDir/appimage/${appImageName}.zsync")
        ].each { sourcePath ->
            copy {
                from sourcePath
                into "$buildDir/jpackage"
            }
        }

        //delete appDirBase
        println "Created AppImage at: $buildDir/jpackage/${appImageName}"
    }
}

task printJpackageContents {
    doLast {
        def jpackageDir = file("$buildDir/jpackage")
        if (jpackageDir.exists() && jpackageDir.isDirectory()) {
            println "Build complete, generated binaries:"

            jpackageDir.eachFile { file ->
                if (file.isFile()) {
                    println "- ${file.absolutePath}"
                }
            }
        } else {
            println "jpackage directory is missing or not a directory: ${jpackageDir.path}"
        }
    }
}

tasks.zipJpackageImage.finalizedBy printJpackageContents
tasks.createAppImage.finalizedBy printJpackageContents
